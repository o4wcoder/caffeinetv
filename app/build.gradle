apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "tv.caffeine.app"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 88
        versionName "0.16 ($versionCode)"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    flavorDimensions "environment"
    productFlavors {
        prod {
            dimension "environment"
            applicationIdSuffix ""
            versionNameSuffix ""
        }
        staging {
            dimension "environment"
            applicationIdSuffix ".staging"
            versionNameSuffix " (staging)"
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"

    // Jetpack
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha03'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.core:core-ktx:1.1.0-alpha05'
    implementation 'androidx.fragment:fragment:1.1.0-alpha05'
    implementation 'androidx.fragment:fragment-ktx:1.1.0-alpha05'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    // Preferences
    implementation 'androidx.preference:preference:1.0.0'

    // Layout
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Image Loading
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'jp.wasabeef:picasso-transformations:2.2.1'

    // Date/Time
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.0'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:${versions.navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${versions.navigation}"

    // Data binding
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    kapt "androidx.databinding:databinding-compiler:${versions.android_plugin}"

    // Retrofit, okhttp
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.okhttp3:okhttp:3.14.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.0'

    // Dagger
    implementation "com.google.dagger:dagger:${versions.dagger}"
    implementation "com.google.dagger:dagger-android:${versions.dagger}"
    implementation "com.google.dagger:dagger-android-support:${versions.dagger}"
    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${versions.dagger}"
    compileOnly 'com.squareup.inject:assisted-inject-annotations-dagger2:0.3.3'
    kapt "com.squareup.inject:assisted-inject-processor-dagger2:0.3.3"
    kaptTest "com.google.dagger:dagger-compiler:${versions.dagger}"
    kaptTest "com.google.dagger:dagger-android-processor:${versions.dagger}"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'
    debugImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-debug:1.1.1'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    // Permissions
    implementation 'pub.devrel:easypermissions:3.0.0'

    // Push notifications
    implementation 'com.google.firebase:firebase-messaging:17.4.0'

    // Crash Reporting, Performance
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.google.firebase:firebase-perf:16.2.4'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // In-App Purchases
    debugImplementation('com.pixiteapps.billingx:billingx:0.8.1') {
        exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk7"
    }
    releaseImplementation 'com.android.billingclient:billing:1.2'

    // Legal, etc.
    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.2'

    // Social
    implementation 'com.facebook.android:facebook-android-sdk:4.41.0'

    // recaptcha
    implementation 'com.google.android.gms:play-services-safetynet:16.0.0'

    // Kochava
    implementation 'com.kochava.base:tracker:3.5.0'

    // ThreatMetrix
    implementation 'commons-io:commons-io:2.6'
    implementation 'commons-codec:commons-codec:1.12'
    implementation fileTree(dir: 'libs', include: ['TrustDefender-5.3-91.aar'])

    // Tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'io.mockk:mockk:1.9.2'
    testImplementation 'android.arch.core:core-testing:1.1.1'
    testImplementation 'org.hamcrest:hamcrest-library:2.1'
    testImplementation "org.robolectric:robolectric:${versions.robolectric}"
    testImplementation 'androidx.test:core:1.1.0'
    testImplementation 'androidx.test:runner:1.1.1'
    testImplementation 'androidx.test:rules:1.1.1'
    debugImplementation 'androidx.fragment:fragment-testing:1.1.0-alpha05'
    testImplementation "androidx.test.espresso:espresso-core:${versions.espresso}"
    testImplementation "androidx.test.espresso:espresso-intents:${versions.espresso}"
    androidTestImplementation 'io.mockk:mockk-android:1.9.2'
    androidTestImplementation 'android.arch.core:core-testing:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.espresso}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${versions.espresso}"
    implementation project(':libwebrtc')
}

apply plugin: 'com.google.gms.google-services'
