// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext.versions = [
        kotlin: '1.3.40',
        android_plugin: '3.5.0-rc01',
        androidx_core: '1.2.0-alpha02',
        appcompat: '1.1.0-rc01',
        navigation: '2.1.0-alpha06',
        fragments: '1.1.0-rc01',
        recyclerview: '1.1.0-beta01',
        viewpager2: '1.0.0-alpha06',
        dagger: '2.23.2',
        coroutines: '1.3.0-M2',
        espresso: '3.2.0',
        lifecycle: '2.2.0-alpha01',
        robolectric: '4.3',
        security: '1.0.0-alpha02'
    ]

    repositories {
        google()
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
            content {
                includeGroupByRegex "io\\.fabric.*"
            }
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.android_plugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:${versions.navigation}"
        classpath 'io.fabric.tools:gradle:1.26.1'
        classpath 'com.google.gms:google-services:4.3.0'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.9.5'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
        classpath 'com.google.api-client:google-api-client:1.25.0'
        classpath('com.google.firebase:firebase-plugins:1.2.0') {
            exclude group: 'com.google.api-client', module: 'google-api-client'
        }
        classpath 'com.apollographql.apollo:apollo-gradle-plugin:1.0.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0-RC15")
}

detekt {
    toolVersion = "1.0.0-RC15"
    input = files("app/src/main/java")
    filters = ".*/resources/.*,.*/build/.*"
    config = files("default-detekt-config.yml")
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://kochava.bintray.com/maven'
            content {
                includeGroupByRegex "com\\.kochava.*"
            }
        }
        maven {
            url "https://dl.bintray.com/apollographql/android"
            content {
                includeGroupByRegex "com\\.apollographql.*"
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
